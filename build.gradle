buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'idea'

version = buildVersionString()
group = "zach2039.oldguns" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "oldguns"

// Taken and modified from https://github.com/BluSunrize/ImmersiveEngineering/
String buildVersionString() {
    String mod_output_version = "${mod_version}-${mod_buildnumber}"
	if (System.getenv("CI") == "true") {
        File changelog = new File("CHANGELOG.md")
        if (!changelog.exists()) {
            // Get the current minor/patch number and bump it
            int idx = mod_version.lastIndexOf(".")
            String major = mod_version.substring(0, idx)
            String minor = mod_version.substring(idx + 1)
            int bumped = minor.toInteger() + 1
            mod_output_version = "${major}.${bumped}-pre"
        } else {
            // If the changelog file is present, we use the first line from that
            String first_line = changelog.readLines().get(0)
            mod_output_version = first_line.substring(first_line.indexOf("-") + 1)
        }
        // Now use describe to get the number of commits since the last tag
        exec {
            commandLine 'git', 'fetch', '--force', '--tags'
        }
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--long', '--abbrev=8', 'HEAD'
            standardOutput = stdout
        }
        String descr = stdout.toString().trim()
        descr = descr.substring(0, descr.length() - 10)
        int idx = descr.lastIndexOf("-")
        int pre_version = descr.substring(idx + 1).toInteger()
		String commit_current = System.getenv("GIT_COMMIT")
		String commit_last = System.getenv("GIT_PREVIOUS_COMMIT")
        // If there have been more than 0 commits, consider this a pre-build
        // otherwise use normal versioning (so a Jenkins build on the tag is equal to a release)
		if (!commit_current.equals(commit_last)) {
            mod_output_version = "${mod_output_version}.${pre_version}"
		}
    }
    return "${mc_version}-${mod_output_version}"
}


sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "snapshot_20171003"
	replace '@VERSION@', project.version
	useDepAts = true
}

repositories {
	maven {
		// location of the maven that hosts JEI files
		name = "Progwml6 maven"
		url = "https://dvs1.progwml6.com/files/maven/"
	}
	maven {
		// location of a maven mirror for JEI files, as a fallback
		name = "ModMaven"
		url = "https://modmaven.k-4u.nl"
	}
    maven { 
		// CraftTweaker (MineTweaker3)
        name 'jared maven'
        url "http://maven.blamejared.com/"
    }
}

dependencies {
	// Crafttweaker stuff
	compileOnly "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.8.468"	
	
	// compile against the JEI API but do not include it at runtime
	deobfProvided "mezz.jei:jei_${mc_version}:${jei_version}:api"
	// at runtime, use the full JEI jar
	runtime "mezz.jei:jei_${mc_version}:${jei_version}"
}

processResources {
	/* Ensure tasks redone with version change. */
	inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    /* Replace stuff in mcmod.info, nothing else. */
    from(sourceSets.main.resources.srcDirs)
    {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    /* Copy everything else except the mcmod.info. */
    from(sourceSets.main.resources.srcDirs)
    {
        exclude 'mcmod.info'
    }
	
	// Move access transformers to META-INF
	rename '(.+_at.cfg)', 'META-INF/$1'
}
